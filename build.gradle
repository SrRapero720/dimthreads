import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'java'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.+'
    id 'com.modrinth.minotaur' version '2.+'
    id 'net.darkhax.curseforgegradle' version '1.1.+'
}

base {
    archivesName = modid
    version = "$modloader-mc$mcversion-v$modversion"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

minecraft {
    accessTransformers {
        file('src/main/resources/META-INF/accesstransformer.cfg')
    }
}

runs {
    configureEach {
        systemProperty  'forge.logging.console.level', 'debug'
        systemProperty  'log4j.appender.Console', "org.apache.log4j.ConsoleAppender"
        systemProperty  'terminal.jline', 'true'

        modSource project.sourceSets.main
    }

    client {
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty 'forge.enabledGameTestNamespaces', modid
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', modid
        programArgument '--nogui'
    }
}

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

repositories {
    mavenCentral()
    maven { url "https://maven.fabricmc.net/" }
    maven { url "https://maven.tterrag.com/" }
    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url "https://jitpack.io" }

    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    implementation "net.neoforged:neoforge:$fgversion"

}

jar {
    manifest {
        attributes([
                "Specification-Title": "${modid}",
                "Specification-Vendor": "${modauthor}",
                "Specification-Version": "${modversion}",
                "Implementation-Title": "${modname}",
                "Implementation-Version": "${modversion}",
                "Implementation-Vendor" : "${modauthor}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }
}

// Process target resources with mod info
tasks.processResources.outputs.upToDateWhen { false }
processResources {
    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta', 'resourcepacks/**/pack.mcmeta', "$modid.$mixin_file_suffix".toString()]) {
        expand rootProject.properties
    }
}

task upload(type: GradleBuild) {
    enabled = System.getenv("MODRINTH_TOKEN") != null && System.getenv("CURSEFORGE_TOKEN") != null
    description 'Uploads new version to CurseForge and Modrinth'
    group = 'publishing'
    tasks = [ 'curseforge', 'modrinth' ]
}

task curseforge(type: TaskPublishCurseForge) {
    disableVersionDetection()
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(curseforgeid, jar)
    mainFile.displayName = getVersionBuildName()
    mainFile.addModLoader("NeoForge")
    mainFile.addGameVersion(mcversion, "server", "client")
    mainFile.addIncompatibility("dimensional-threading-reforged")
    mainFile.changelog = getChangelogText(false)
    mainFile.changelogType = "markdown"
    mainFile.releaseType = versiontype
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = modrinthid

    uploadFile = jar
    versionName = getVersionBuildName()
    versionNumber = version.toString()
    versionType = versiontype
    gameVersions = [ mcversion ]
    changelog = getChangelogText(true)
    loaders = [ modloader.toLowerCase() ]
}

String getVersionBuildName() {
    return "$modname/${modloader.toLowerCase().capitalize()}/mc$mcversion/v$modversion"
}

String getChangelogText(boolean latest) {
    String result = ''
    for (String line: file('CHANGELOG.md').readLines('UTF-8')) {
        if (latest && line.isEmpty()) return result
        result += line + "\n"
    }
    return result
}

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}